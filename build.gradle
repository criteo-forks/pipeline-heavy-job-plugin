plugins {
    id "org.jenkins-ci.jpi" version "0.43.0"
    id "maven-publish"
    id "com.palantir.git-version" version "0.15.0"
}

ext {
  coreBaseVersion = '2.164'
  corePatchVersion = '3'
  coreBomVersion = '10'
}

group = "ru.funbox.jenkins.plugins"

version = gitVersion()

description = """This plugin allows to define job weight for pipeline projects.
        This makes it possible to use server resources more efficiently."""

jenkinsPlugin {
    jenkinsVersion = "${coreBaseVersion}.${corePatchVersion}"
    shortName = "pipeline-heavy-job"
    displayName = "Pipeline HeavyJob Plugin"
}

dependencies {
    api platform("io.jenkins.tools.bom:bom-${coreBaseVersion}.x:${coreBomVersion}")
    implementation 'org.jenkins-ci.plugins:structs'
    implementation 'org.jenkins-ci.plugins:durable-task'
    implementation 'org.jenkins-ci.modules:windows-slave-installer:1.12'
    implementation 'org.jenkinsci.plugins:pipeline-model-definition:1.6.0'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-step-api'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-cps'
    implementation 'org.jenkins-ci.plugins.workflow:workflow-durable-task-step'
    testRuntimeOnly "org.jenkins-ci.main:jenkins-war:${coreBaseVersion}"
    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:2.62'
    testImplementation 'org.jenkins-ci:test-annotations:1.3'
    testImplementation 'junit:junit:4.13'
}

publishing {
   repositories {
    maven {
      name = "GitHubPackages"
      url = "https://maven.pkg.github.com/criteo-forks/pipeline-heavy-job-plugi"
      credentials {
        username = System.getenv("GITHUB_ACTOR")
        password = System.getenv("GITHUB_TOKEN")
      }
    }
  }
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
